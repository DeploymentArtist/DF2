<job id="Install-VirtualCloneDrive">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

'//----------------------------------------------------------------------------
'// Solution: Hydration
'// Purpose: Used to install Virtual Clone Drive 
'// Usage: cscript Install-VirtualCloneDrive.wsf [/debug:true]
'// Version: 1.0 - 27 May 2011 - Mikael Nystrom
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 
Dim sApplicationName

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sFile1
	Dim sFile2

	sFile1 = oUtility.ScriptDir & "\source\VCDRootCert.cer.p7b"
	sFile2 = oUtility.ScriptDir & "\source\SetupVirtualCloneDrive5450.exe"

	sApplicationName = "Virtual Clone Drive"

	oLogging.CreateEntry "Install-" & sApplicationName & ": Starting " & sApplicationName & " installation", LogTypeInfo
	oLogging.CreateEntry "Install-" & sApplicationName & ": Starting " & sApplicationName & " -Certificate Trust", LogTypeInfo
	

	If not oFSO.FileExists(sFile1) then
		oLogging.CreateEntry "Install-" & sApplicationName & ": " & sFile1 & " was not found, unable to install " & sApplicationName & "", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	oLogging.CreateEntry "certutil.exe -addstore -Enterprise ""TrustedPublisher"" """ & sFile1 & """", LogTypeInfo
	
	iRetVal = oUtility.RunWithHeartbeat("certutil.exe -addstore -Enterprise ""TrustedPublisher"" """ & sFile1 & """")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
	Else 
		ZTIProcess = Failure
	End If
	
	oLogging.CreateEntry "Install-" & sApplicationName & ": Return code from command = " & iRetVal, LogTypeInfo
	oLogging.CreateEntry "Install-" & sApplicationName & ": Starting " & sApplicationName & " -Main Application", LogTypeInfo
	
	
	If not oFSO.FileExists(sFile2) then
		oLogging.CreateEntry "Install-" & sApplicationName & ": " & sFile2 & " was not found, unable to install " & sApplicationName & "", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	oLogging.CreateEntry "" & sFile2 & " /S", LogTypeInfo
	
	iRetVal = oUtility.RunWithHeartbeat("""" & sFile2 & """ /S")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
	Else 
		ZTIProcess = Failure
	End If
	
	oLogging.CreateEntry "Install-" & sApplicationName & ": Return code from command = " & iRetVal, LogTypeInfo
	
	oLogging.CreateEntry "Install-" & sApplicationName & ": Finished " & sApplicationName & " installation", LogTypeInfo
	
End Function 

</script>
</job>